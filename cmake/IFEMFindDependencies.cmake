# Locate the IFEM dependencies

find_package(CBLAS REQUIRED)
find_package(LAPACK REQUIRED)

find_package(SISL QUIET)
find_package(GoToolsCore QUIET)
find_package(GoTrivariate QUIET)

if(TARGET GoToolsCore)
  add_library(GoTools::GoToolsCore IMPORTED INTERFACE)
  string(REPLACE "." ";" GoVersion ${GoToolsCore_VERSION})
  list(GET GoVersion 0 GoToolsCore_VERSION_MAJOR)
  target_compile_definitions(GoTools::GoToolsCore
                             INTERFACE
                               GoTools_VERSION_MAJOR=${GoToolsCore_VERSION_MAJOR}
                               GOTOOLS_HAS_BASISDERIVS_SF3=1
  )
  target_link_libraries(GoTools::GoToolsCore INTERFACE GoToolsCore)
  add_library(GoTools::GoTrivariate IMPORTED INTERFACE)
  target_link_libraries(GoTools::GoTrivariate INTERFACE GoTrivariate GoTools::GoToolsCore)
else()
  find_package(GoTools REQUIRED)
  add_library(GoTools::GoToolsCore INTERFACE IMPORTED)
  set(GoTools_LIBRARY_DIRS ${GoTools_LIBRARIES})
  list(FILTER GoTools_LIBRARY_DIRS INCLUDE REGEX -L.*)
  list(TRANSFORM GoTools_LIBRARY_DIRS REPLACE "-L" "")
  list(FILTER GoTools_LIBRARIES EXCLUDE REGEX -L.*)
  target_link_libraries(GoTools::GoToolsCore INTERFACE ${GoTools_LIBRARIES})
  target_include_directories(GoTools::GoToolsCore INTERFACE ${GoTools_INCLUDE_DIRS})
  target_compile_definitions(
    GoTools::GoToolsCore INTERFACE ${GoTools_CXX_FLAGS} GoTools_VERSION_MAJOR=4)
  target_link_directories(GoTools::GoToolsCore INTERFACE ${GoTools_LIBRARY_DIRS})
  include(CheckTypeSize)
  set(CMAKE_EXTRA_INCLUDE_FILES "GoTools/geometry/SplineSurface.h")
  set(CMAKE_REQUIRED_FLAGS -std=c++11)
  check_type_size(Go::BasisDerivsSf3 HAS_BD3 LANGUAGE CXX)
  if(HAS_BD3)
    target_compile_definitions(GoTools::GoToolsCore INTERFACE GOTOOLS_HAS_BASISDERIVS_SF3=1)
  endif()

  add_library(GoTools::GoTrivariate INTERFACE IMPORTED)
  set(GoTrivariate_LIBRARY_DIRS ${GoTrivariate_LIBRARIES})
  list(FILTER GoTrivariate_LIBRARY_DIRS INCLUDE REGEX -L.*)
  list(TRANSFORM GoTrivariate_LIBRARY_DIRS REPLACE "-L" "")
  list(FILTER GoTrivariate_LIBRARIES EXCLUDE REGEX -L.*)
  target_include_directories(GoTools::GoTrivariate INTERFACE ${GoTrivariate_INCLUDE_DIRS})
  target_link_libraries(GoTools::GoTrivariate INTERFACE ${GoTrivariate_LIBRARIES} GoTools::GoToolsCore)
  target_link_directories(GoTools::GoTrivariate INTERFACE ${GoTrivariate_LIBRARY_DIRS})
endif()

find_package(ARPACK REQUIRED)
find_package(TinyXML2 REQUIRED)

if(IFEM_USE_SUPERLU OR IFEM_USE_SUPERLU_MT)
  find_package(SuperLU)
endif()

if(IFEM_USE_LRSPLINES)
  find_package(LRSpline)
  if(LRSpline_FOUND AND NOT TARGET LRSpline::LRSpline)
    add_library(LRSpline::LRSpline INTERFACE IMPORTED)
    set(LRSpline_LIBRARY_DIRS ${LRSpline_LIBRARIES})
    list(FILTER LRSpline_LIBRARY_DIRS INCLUDE REGEX -L.*)
    list(TRANSFORM LRSpline_LIBRARY_DIRS REPLACE "-L" "")
    list(FILTER LRSpline_LIBRARIES EXCLUDE REGEX -L.*)
    target_link_libraries(LRSpline::LRSpline INTERFACE ${LRSpline_LIBRARIES})
    target_include_directories(LRSpline::LRSpline INTERFACE ${LRSpline_INCLUDE_DIRS})
    target_compile_definitions(LRSpline::LRSpline INTERFACE ${LRSpline_DEFINITIONS})
    target_link_directories(LRSpline::LRSpline INTERFACE ${LRSpline_LIBRARY_DIRS})
  endif()
endif()

if(IFEM_USE_MPI)
  find_package(MPI REQUIRED)
endif()

if(IFEM_USE_PETSC)
  find_package(PETSc)
endif()

if(IFEM_USE_SLEPC AND TARGET PETSc::PETSc)
  find_package(SLEPc)
endif()

if(IFEM_USE_HDF5)
  if(MPI_FOUND)
    set(HDF5_PREFER_PARALLEL 1)
  endif()
  find_package(HDF5 COMPONENTS C)
endif()

if(NOT IFEM_USE_SPR MATCHES "OFF")
  if(IFEM_USE_SPR MATCHES "I64")
    set(SPR_USE_INT8 TRUE)
  else()
    set(SPR_USE_INT4 TRUE)
  endif()
  find_package(SPR)
endif()

if(IFEM_USE_SAMG)
  find_package(SAMG)
endif()

if(IFEM_USE_VTFWRITER)
  find_package(VTFWriter)
endif()

if(IFEM_USE_OPENMP)
  find_package(OpenMP)
endif()

if(IFEM_USE_UMFPACK)
  find_package(SuiteSparse COMPONENTS umfpack)
endif()

if(IFEM_USE_ISTL)
  find_package(ISTL)
endif()

if(IFEM_USE_CEREAL)
  find_path(CEREAL_INCLUDE_DIRS NAMES cereal/cereal.hpp)
  if(CEREAL_INCLUDE_DIRS)
    set(cereal_FOUND ON)
    add_library(cereal::cereal IMPORTED INTERFACE)
    target_include_directories(cereal::cereal INTERFACE ${CEREAL_INCLUDE_DIRS})
  endif()
endif()

if(IFEM_USE_ZOLTAN)
  find_package(Zoltan)
endif()

if(IFEM_USE_TRACY)
  find_package(Tracy)
endif()

# Export some variables used for tests enablement
get_directory_property(hasParent PARENT_DIRECTORY)
if(hasParent)
  foreach(VAR ARPACK cereal HDF5 ISTL LRSpline MPI PETSc SLEPc SPR VTFWriter Zoltan)
    set(${VAR}_FOUND ${${VAR}_FOUND} PARENT_SCOPE)
  endforeach()
endif()
